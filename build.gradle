plugins {
    id "java"
    id "application"
}

/*
 * Set Java source code version.
 */
sourceCompatibility = JavaVersion.VERSION_1_8

/*
 * Name of the main class of the application
 */
mainClassName = "de.comsystoreply.spring.core.bootcamp.Application"

/*
 * Set repositories used to resolve dependencies.
 */
repositories {
    jcenter()
    mavenCentral()
}

dependencies {

    /*
     * Spring MVC for REST controller
     */
    final springVersion = '5.0.9.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion

    /*
     * Spring testing support
     */
    testCompile group: 'org.springframework', name: 'spring-test', version: springVersion

    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.0'

    /*
     * Embedded Tomcat web server to run the application on.
     */
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: '9.0.12'

    /*
   * Jackson to serialize and deserialize JSON
   */
    final jacksonVersion = '2.9.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: jacksonVersion

    compile group: 'org.springframework.data', name: 'spring-data-jpa', version: '2.1.1.RELEASE'

    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.2.Final'
    runtime group: 'org.hibernate', name: 'hibernate-java8', version: '5.2.13.Final'

    runtime group: 'com.h2database', name: 'h2', version: '1.4.196'

    /*
     * Logback behind SLF4J for logging
     */
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    /*
     * JUnit test framework
     */
    final junitVersion = '5.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

    /*
     * Library with matchers to be used as part of test cases
     */
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'

    /*
     * JsonPath used to evaluate values in JSON responses as part of test cases
     */
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.4.0'
}

test {
    useJUnitPlatform()
}